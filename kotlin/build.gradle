buildscript {
    ext.kotlin_version = '1.1.2'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.github.kropp.teamcity-gitversion'
apply plugin: 'com.jfrog.bintray'

repositories {
    mavenCentral()
}

jar {
    baseName = 'jsonld-metadata-kotlin'
}

if (System.getenv('BINTRAY_USER')) {
    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')
        pkg {
            repo = 'org.schema'
            name = 'jsonld-metadata-kotlin'
            userOrg = 'kropp'
            licenses = ['Apache-2.0']
            vcsUrl = 'git@github.com:kropp/jsonld-metadata.git'
            version {
                name = project.version
                desc = 'JSON-LD Metadata Kotlin ' + project.version
                vcsTag = project.version
            }
        }
        publications = ['JsonLdMetadataKotlinPublication']
    }
}

publishing {
    publications {
        JsonLdMetadataKotlinPublication(MavenPublication) {
            artifact jar
            groupId 'org.schema'
            artifactId 'jsonld-metadata-kotlin'
            version project.version
        }
    }
}

if (hasProperty('mavenPublishUrl')) {
    uploadArchives {
        repositories {
            mavenDeployer {

                repository(url: project.mavenPublishUrl) {
                }

                pom.project {
                    groupId 'org.schema'
                    artifactId 'jsonld-metadata-kotlin'

                    name 'JSON-LD Metadata'
                    description 'JSON-LD serializer Kotlin extensions'
                    packaging 'jar'
                    url 'https://github.com/kropp/jsonld-metadata/'

                    scm {
                        url 'https://github.com/kropp/jsonld-metadata/'
                        connection 'scm:git:git@github.com:kropp/jsonld-metadata.git'
                        developerConnection 'scm:git:git@github.com:kropp/jsonld-metadata.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'JetBrains'
                            name 'JetBrains Team'
                            organization 'JetBrains s.r.o'
                            organizationUrl 'http://www.jetbrains.com'
                        }
                    }
                }
            }
        }
    }
}

dependencies {
    compile rootProject
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile 'junit:junit:4.11'
}
